//Firkaarg's Hideout //////////////

IF
   Global("Iathing","AR1203",0)
   Global("FirkraagDead","GLOBAL",0)
   Dead("firkra02")
THEN
   RESPONSE #100
      SetGlobal("Iathing","AR1203",1)
      SetGlobal("FirkraagDead","GLOBAL",1)
      SetGlobalTimer("Iavanyafir","GLOBAL",SEVEN_DAYS)
END

IF
  Global("Ianticheat","AR1203",0)
  Global("FirkraagDead","GLOBAL",0)
  Exists("firkra02") //
  !Dead("firkra02") //
  !Allegiance("firkra02",ENEMY)
  HPPercentLT("firkra02",100)
THEN
  RESPONSE #100
    SetGlobal("Ianticheat","AR1203",1)
    ChangeEnemyAlly("firkra02",ENEMY)
    ApplySpellRES("SPWII07","firkra02")
END

IF
   Global("VanyaJob","GLOBAL",1)
   Global("Iacreature","AR1203",0)
   OR(2)
    Global("FirkraagDead","GLOBAL",1)
    Dead("firkra02")
THEN
   RESPONSE #100
      SetGlobal("Iacreature","AR1203",1)
      TriggerActivation("Tran1202",FALSE)
      CreateCreature("S!rune2",[1113.1047],5)  //
      CreateCreature("tanthf01",[1182.990],3)  //
      CreateCreature("tanthf01",[1031.1111],9)  //
      CreateCreature("tanthf01",[936.1176],11)  //
      CreateCreature("tanthf01",[866.1231],13)  //
      CreateCreature("tanthf01",[1210.1085],5)  //
      CreateCreature("tanthf01",[1102.1162],5)  //
      CreateCreature("tanthf01",[979.1252],9)  //
      CreateCreature("tanthf01",[1323.1231],9)  //
      CreateCreature("S!rune3",[1142.1345],13)  //
      CreateCreature("S!rune1",[1421.1368],9)  //
      CreateCreature("S!rune3",[1458.1233],7)  //
      CreateCreature("S!rune2",[1178.1412],13)  //
END

IF
   Global("Iathing","AR1203",1)
   Global("Iacreature","AR1203",1)
   Global("VanyaJob","GLOBAL",1)
   Dead("S!rune1")
   CombatCounter(0)
THEN
   RESPONSE #100
      SetGlobal("Iathing","AR1203",2)
      SetGlobal("VanyaJob","GLOBAL",2)
      SetGlobalTimer("Iavanyasec","GLOBAL",SEVEN_DAYS)
      TriggerActivation("Tran1202",TRUE)
END

IF
  OR(3)
   SpellCast([PC],WIZARD_DEATH_FOG)
   SpellCast([PC],WIZARD_CLOUDKILL)
   SpellCast([PC],WIZARD_STINKING_CLOUD)
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR1203",1)
    SetGlobalTimer("Iacloudtime","AR1203",60)
END

IF
  Global("Iapccloud","AR1203",1)
  GlobalTimerExpired("Iacloudtime","AR1203")
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR1203",0)
END

IF
  Global("Iaexit","AR1203",0)
  Global("Iacantexit","AR1203",1)
THEN
  RESPONSE #100
    SetGlobal("Iaexit","AR1203",1)
    TriggerActivation("Tran1202",FALSE)
END

IF
  Global("Iaexit","AR1203",1)
  Dead("firkra02")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("Iaexit","AR1203",2)
    TriggerActivation("Tran1202",TRUE)
END

IF
  Global("RuneBattleStart","GLOBAL",0)
  Global("Iacreature","AR1203",1)
  InMyArea(Player1)
  SpellCastInnate([PC],0)
THEN
  RESPONSE #100
    SetGlobal("RuneBattleStart","GLOBAL",1)
END

IF
  InMyArea(Player1)
  SpellCast([PC],WIZARD_MIRROR_IMAGE)
THEN
  RESPONSE #100
    SetGlobal("Iapcmimage","AR1203",1)
    SetGlobalTimer("Iaimagetime","AR1203",138)
END

IF
  Global("Iapcmimage","AR1203",1)
  GlobalTimerExpired("Iaimagetime","AR1203")
THEN
  RESPONSE #100
    SetGlobal("Iapcmimage","AR1203",0)
END

IF
   InMyArea(Player1)
   StateCheck(Player1,STATE_SILENCED)
   OR(3)
    Class(Player1,MAGE_ALL)
    Class(Player1,BARD_ALL)
    CheckStat(Player1,1,SCRIPTINGSTATE16)
   Global("Iadisabled1","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled1","AR1203",1)
      ActionOverride(Player1,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player1)
   !StateCheck(Player1,STATE_SILENCED)
   Global("Iadisabled1","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled1","AR1203",0)
END

IF
   InMyArea(Player2)
   StateCheck(Player2,STATE_SILENCED)
   OR(3)
    Class(Player2,MAGE_ALL)
    Class(Player2,BARD_ALL)
    CheckStat(Player2,1,SCRIPTINGSTATE16)
   Global("Iadisabled2","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled2","AR1203",1)
      ActionOverride(Player2,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player2)
   !StateCheck(Player2,STATE_SILENCED)
   Global("Iadisabled2","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled2","AR1203",0)
END

IF
   InMyArea(Player3)
   StateCheck(Player3,STATE_SILENCED)
   OR(3)
    Class(Player3,MAGE_ALL)
    Class(Player3,BARD_ALL)
    CheckStat(Player3,1,SCRIPTINGSTATE16)
   Global("Iadisabled3","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled3","AR1203",1)
      ActionOverride(Player3,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player3)
   !StateCheck(Player3,STATE_SILENCED)
   Global("Iadisabled3","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled3","AR1203",0)
END

IF
   InMyArea(Player4)
   StateCheck(Player4,STATE_SILENCED)
   OR(3)
    Class(Player4,MAGE_ALL)
    Class(Player4,BARD_ALL)
    CheckStat(Player4,1,SCRIPTINGSTATE16)
   Global("Iadisabled4","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled4","AR1203",1)
      ActionOverride(Player4,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player4)
   !StateCheck(Player4,STATE_SILENCED)
   Global("Iadisabled4","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled4","AR1203",0)
END

IF
   InMyArea(Player5)
   StateCheck(Player5,STATE_SILENCED)
   OR(3)
    Class(Player5,MAGE_ALL)
    Class(Player5,BARD_ALL)
    CheckStat(Player5,1,SCRIPTINGSTATE16)
   Global("Iadisabled5","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled5","AR1203",1)
      ActionOverride(Player5,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player5)
   !StateCheck(Player5,STATE_SILENCED)
   Global("Iadisabled5","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled5","AR1203",0)
END

IF
   InMyArea(Player6)
   StateCheck(Player6,STATE_SILENCED)
   OR(3)
    Class(Player6,MAGE_ALL)
    Class(Player6,BARD_ALL)
    CheckStat(Player6,1,SCRIPTINGSTATE16)
   Global("Iadisabled6","AR1203",0)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled6","AR1203",1)
      ActionOverride(Player6,ReallyForceSpellRES("spwii44",Myself))
END

IF
   InMyArea(Player6)
   !StateCheck(Player6,STATE_SILENCED)
   Global("Iadisabled6","AR1203",1)
THEN
   RESPONSE #100
      SetGlobal("Iadisabled6","AR1203",0)
END

IF
  InMyArea(Player1)
  OR(2)
   Global("Iamessage","AR1203",1)
   Global("Iamessage","AR1203",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamessage","AR1203",1)
    DisplayStringNoName(Player1,16297)
END