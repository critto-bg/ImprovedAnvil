IF
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  !GlobalTimerNotExpired("Iarage","LOCALS")
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  Global("Iapcastts","GLOBAL",0)
  CheckStatGT(Myself,0,SCRIPTINGSTATE9)
  !CheckStat(Myself,1,SCRIPTINGSTATE4)
  See(NearestEnemyOf(Myself))
  GlobalLT("berserk","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("berserk","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    SetGlobalTimer("Iarage","LOCALS",32)
    ForceSpell(Myself,BARBARIAN_RAGE)
END

IF
  Global("RiatavinFight","GLOBAL",1)
  Allegiance(Myself,NEUTRAL)
THEN
  RESPONSE #100
    Enemy()
END

IF
  Global("RiatavinFight","GLOBAL",0)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    SetGlobal("RiatavinFight","GLOBAL",1)
END

IF
  InMyArea(Player1)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Global("Iatreasure","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Alignment(Player1,MASK_GOOD)
  Class(Player1,MAGE_ALL)
  !Class(Player1,FIGHTER_ALL)
  !Class(Player1,THIEF_ALL)
  !Class(Player1,CLERIC_ALL)
  CheckStat(Player1,6,SCRIPTINGSTATE1)
THEN
  RESPONSE #100
    SetGlobal("Iatreasure","LOCALS",1)
    GiveItemCreate("S!misc52",Myself,0,0,0) //
END

IF
  Global("Iatreasure","LOCALS",0)
  Allegiance(Myself,ENEMY)
  OR(6)
   !Class(Player1,MAGE_ALL)
   !Alignment(Player1,MASK_GOOD)
   Class(Player1,FIGHTER_ALL)
   Class(Player1,THIEF_ALL)
   Class(Player1,CLERIC_ALL)
   !CheckStat(Player1,6,SCRIPTINGSTATE1)
THEN
  RESPONSE #100
    SetGlobal("Iatreasure","LOCALS",1)
END

IF
  Allegiance(Myself,NEUTRAL)
  OR(8)
    HPPercentLT(Myself,100)
    AttackedBy([GOODCUTOFF],DEFAULT)
    HitBy([GOODCUTOFF],CRUSHING)
    SpellCastOnMe([GOODCUTOFF],0)
    SpellCastOnMe([PC],0)
    TookDamage()
    SpellCastInnate([GOODCUTOFF],SET_SNARE_TRAP)
    SpellCastInnate([GOODCUTOFF],SET_SPECIAL_SNARE_TRAP)
  Global("RiatavinFight","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("RiatavinFight","GLOBAL",1)
    Enemy()
    Shout(151)
END

IF
  Heard([EVILCUTOFF],151)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Enemy()
END

IF
  InMyArea(Player1)
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_STUNNED)
  Global("Iapcastts","GLOBAL",0)
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("POTN55",Myself) // ~Potion of Superior Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN55",Myself) // ~Potion of Superior Healing~
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),90,RESISTSLASHING)
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  RandomNumGT(6,4)
  Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END