IF
  OR(2)
   AttackedBy([GOODCUTOFF],DEFAULT)
   TookDamage()
  Allegiance(Myself,NEUTRAL)
  InMyArea(Player1)
THEN
  RESPONSE #100
    Enemy()
    Shout(99)
    MoveToObject(Player1)
END

IF
  Exists("tarnor")
  !Dead("tarnor")
  Allegiance("tarnor",NEUTRAL)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    Shout(99)
    ActionOverride("tarnor",Enemy())
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  See([PC])
  HasItem("potn35",Myself)
  Global("Iashielding","LOCALS",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iashielding","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn35",Myself) //
END

IF
  Detect([PC])
  Global("Iatreasure","LOCALS",0)
  Global("Iaharmoniumplace","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("Iaharmoniumplace","GLOBAL",5)
    SetGlobal("Iatreasure","LOCALS",1)
    GiveItemCreate("wa2halb",Myself,0,0,0)  // Harmonium Halberd +3
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("Iashielding","LOCALS",1)
  HasItem("S!minhp1",Myself)
THEN
  RESPONSE #100
    DestroyItem("S!minhp1")
END

IF
  !HasItem("S!minhp1",Myself)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  See([PC])
  HasItem("potn14",Myself)
  Global("Iaspeed","LOCALS",0)
  Global("Iapcastts","GLOBAL",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iaspeed","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn14",Myself) //
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  Global("Iapcastts","GLOBAL",0)
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,60)
  HasItem("potn52",Myself)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn52",Myself)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),4)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Exists("tarnor")
  !Dead("tarnor")
  Allegiance("tarnor",ENEMY)
  Allegiance(Myself,NEUTRAL)
  InMyArea(Player1)
THEN
  RESPONSE #100
    Enemy()
    Shout(99)
    MoveToObject(Player1)
END

IF
  Exists("gaius")
  !Dead("gaius")
  Allegiance("gaius",ENEMY)
  Allegiance(Myself,NEUTRAL)
  InMyArea(Player1)
THEN
  RESPONSE #100
    Enemy()
    Shout(99)
    MoveToObject(Player1)
END
