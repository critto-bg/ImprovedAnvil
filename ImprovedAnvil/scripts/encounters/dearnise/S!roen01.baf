IF
  Global("KeepPlot","GLOBAL",11)
  Global("Iapcastts","GLOBAL",0)
  AreaCheck("AR1304")
  See([PC])
  Allegiance(Myself,NEUTRAL)
THEN
  RESPONSE #100
    StartDialogueNoSet([PC])
END

IF
  Global("Iaroelord02","LOCALS",0)
  Allegiance(Myself,ENEMY)
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    Shout(ALERT)
    MoveToObject(Player1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Detect([PC])
  Global("Iatreasure","LOCALS",0)
  Global("Iasentinelplace","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("Iasentinelplace","GLOBAL",5)
    SetGlobal("Iatreasure","LOCALS",1)
    GiveItemCreate("shld22",Myself,0,0,0)  // Sentinel +4
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  HasItem("S!minhp1",Myself)
THEN
  RESPONSE #100
    DestroyItem("S!minhp1")
END

IF
  !HasItem("S!minhp1",Myself)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  GlobalGT("Iapcastts","GLOBAL",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("Iashout","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iashout","LOCALS",1)
    Shout(ALERT)
END

IF
  Allegiance(Myself,ENEMY)
  HPPercentLT(Myself,80)
  See(NearestEnemyOf(Myself))
  Global("Iaroelord05","LOCALS",0)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobal("Iaroelord05","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_HARDINESS)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,60)
  HasItem("potn55",Myself)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)//quaffs a potion
    UseItem("potn55",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),5)
  GlobalLT("Iaroelord02","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iaroelord02","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",7)
    ForceSpell(Myself,WARRIOR_GREATER_WHIRLWIND)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  Range(LastSeenBy(Myself),5)
  GlobalLT("Iaroelord03","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iaroelord03","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",7)
    ForceSpell(Myself,WARRIOR_CRITICAL_STRIKE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),5)
  Global("Iaroelord04","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaroelord04","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE5)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  Range(LastSeenBy(Myself),5)
  Global("Iaroelord06","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iaroelord06","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_POWER_ATTACK)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Iashout","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",90)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Iashout","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("Iashout","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iashout","LOCALS",0)
    SetGlobal("Iaroelord02","LOCALS",0)
    SetGlobal("Iaroelord03","LOCALS",0)
    SetGlobal("Iaroelord04","LOCALS",0)
    SetGlobal("Iaroelord05","LOCALS",0)
    SetGlobal("Iaroelord06","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),90,RESISTSLASHING)
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
  RandomNumGT(6,4)
  OR(2)
   AttackedBy(LastSeenBy(Myself),MELEE)
   Range(LastSeenBy(Myself),5)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),1,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Global("KeepPlot","GLOBAL",11)
  See("kpsold03")  // ~de'Arnise Guard~
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    AttackOneRound("kpsold03")
    AttackOneRound("kpsold03")
END

IF
  Global("KeepPlot","GLOBAL",11)
  See("kpsold04")  // ~de'Arnise Guard~
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    AttackOneRound("kpsold04")
    AttackOneRound("kpsold04")
END

IF
  Global("KeepPlot","GLOBAL",11)
  See("kpcapt02")  // ~Captain Cernick~
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    AttackOneRound("kpcapt02")
    AttackOneRound("kpcapt02")
END

IF
  Allegiance(Myself,ENEMY)
  !See([GOODCUTOFF])
  InMyArea(Player1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END
