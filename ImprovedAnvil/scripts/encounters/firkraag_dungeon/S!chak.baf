// Fighter Druid Levels 20 17 //
// First Level Spells (9): CLW x6, Armor of Faith x3 //
// Second Level Spells (9):  //
// Third Level Spells (8): Zone of Sweet Air x3, Miscast Magic x3, CMW x2 //
// Fourth Level Spells (8): Call woodland beings x2, CSW x4, Pro lightning, Acid Shield //
// Fifth Level Spells (5): Iron skins x2, True seeing, Insect Plague //
// Sixth Level Spells (3): Heal, Physical Mirror, conjure Animals //
// Seventh Level Spells (2): Creeping Doom, Shield of the Archons //

IF
  Allegiance(Myself,ENEMY)
  Global("SamiaBattle","AR1202",0)
THEN
  RESPONSE #100
    SetGlobal("SamiaBattle","AR1202",1)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("SamiaBattle","AR1202",1)
THEN
  RESPONSE #100
    Enemy()
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  Detect([PC])
  Global("Iachak01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak01","LOCALS",1)
    ReallyForceSpell(Myself,CLERIC_ARMOR_OF_FAITH)
    ReallyForceSpell(Myself,CLERIC_IRONSKIN)
    ReallyForceSpell(Myself,CLERIC_PROTECTION_FROM_LIGHTNING)
    ReallyForceSpell(Myself,CLERIC_SHIELD_OF_THE_ARCHONS)
    ReallyForceSpell(Myself,CLERIC_PHYSICAL_MIRROR)
    ReallyForceSpell(Myself,CLERIC_ACID_SHIELD)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("Iachak01","LOCALS",1)
  HasItem("BHAALHP1",Myself)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    ReallyForceSpellRES("SPWII07",Myself)
    DestroyItem("BHAALHP1")
    SetInterrupt(TRUE)
END

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  OR(2)
   Detect([GOODCUTOFF])
   Detect([PC])
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("Iasamiadamaged","AR1202",0)
  OR(5)
   AttackedBy([GOODCUTOFF],DEFAULT)
   AttackedBy([PC],DEFAULT)
   SpellCastOnMe([GOODCUTOFF],0)
   SpellCastOnMe([PC],0)
   TookDamage()
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("Iasamiadamaged","AR1202",1)
    Shout(151)
    MoveToObject(Player1)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("Iasamiadamaged","AR1202",1)
  Global("Iahostile","LOCALS",0)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("Iahostile","LOCALS",1)
    Shout(151)
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_INVISIBLE)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  Global("Iapcastts","GLOBAL",0)
  HasItem("POTN10",Myself)  //
  See(NearestEnemyOf(Myself))
  HPPercentLT(Myself,40)
THEN
  RESPONSE #100
    SetGlobal("Iachak06","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("POTN10",Myself)  //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  OR(4)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
   StateCheck(Myself,STATE_HELPLESS)
   StateCheck(Myself,STATE_STUNNED)
   Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  Global("Iachak06","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iachak06","LOCALS",2)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_HEAL)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  CheckStatLT(Myself,1,STONESKINS)
  HPPercentLT(Myself,100)
  Global("Iachak03","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak03","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_IRONSKIN)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  CheckStatLT(Myself,52,RESISTSLASHING)
  GlobalLT("Iaarmor","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iaarmor","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_ARMOR_OF_FAITH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_HELPLESS)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),5)
  HPPercentLT(Myself,90)
  CheckStatLT(Myself,70,RESISTSLASHING)
  GlobalLT("Iahard","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iahard","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpell(Myself,WARRIOR_HARDINESS)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !GlobalTimerNotExpired("truesight","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("truesight","LOCALS")
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  Detect([PC])
  !See([PC])
  Global("Iachak02","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak02","LOCALS",1)
    SetGlobalTimer("truesight","LOCALS",60)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_TRUE_SIGHT)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_HELPLESS)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  InMyArea(Player1)
  Global("Iapccloud","AR1202",1)
  GlobalLT("Iachak14","LOCALS",3)
  Global("Iawhosweet","AR1202",2)
THEN
  RESPONSE #100
    SetGlobal("Iapccloud","AR1202",0)
    IncrementGlobal("Iachak14","LOCALS",1)
    SetGlobal("Iawhosweet","AR1202",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  Global("Iachak04","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak04","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  Global("Iachak04","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak04","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  Global("Iachak04","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak04","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(FourthNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  Global("Iachak04","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak04","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_CREEPING_DOOM)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !StateCheck(Myself,STATE_HELPLESS)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iacstime","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(NearestEnemyOf(Myself),4)
  CheckStatLT(LastSeenBy(Myself),90,RESISTSLASHING)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacstrike","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacstrike","LOCALS",1)
    SetGlobalTimer("Iacstime","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpell(Myself,WARRIOR_CRITICAL_STRIKE)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,70)
  GlobalLT("Iacsw","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacsw","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_SERIOUS_WOUNDS)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,70)
  GlobalLT("Iacmw","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iacmw","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_MEDIUM_WOUNDS)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iammiscast","LOCALS")
  See(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iammiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iammiscast","LOCALS")
  See(SecondNearestEnemyOfType([PC.0.0.MAGE_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iammiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iadmiscast","LOCALS")
  See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iadmiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iadmiscast","LOCALS")
  See(SecondNearestEnemyOfType([PC.0.0.DRUID_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iadmiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iacmiscast","LOCALS")
  See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  !Class(LastSeenBy(Myself),MAGE_ALL)
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iacmiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iacmiscast","LOCALS")
  See(SecondNearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  !Class(LastSeenBy(Myself),MAGE_ALL)
  GlobalLT("Iamiscast","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iamiscast","LOCALS",1)
    SetGlobalTimer("Iacmiscast","LOCALS",66)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_MISCAST_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Iachak07","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak07","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CONJURE_ANIMALS)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  Global("Iachak08","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak08","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  Global("Iachak08","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak08","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  Global("Iachak08","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak08","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(FourthNearest([PC]))
  OR(3)
   Class(LastSeenBy(Myself),DRUID_ALL)
   Class(LastSeenBy(Myself),CLERIC_ALL)
   Class(LastSeenBy(Myself),MAGE_ALL)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE9)
  Global("Iachak08","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iachak08","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),CLERIC_INSECT_PLAGUE)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  GlobalLT("Iachak12","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iachak12","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CALL_WOODLAND_BEINGS)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HPPercentLT(Myself,80)
  GlobalLT("Iaclw","LOCALS",6)
THEN
  RESPONSE #100
    IncrementGlobal("Iaclw","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,CLERIC_CURE_LIGHT_WOUNDS)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Iachak01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",180)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  InMyArea(Player1)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  Allegiance(Myself,ENEMY)
  Global("Iachak01","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("Iachak01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iachak01","LOCALS",0)
    SetGlobal("Iachak02","LOCALS",0)
    SetGlobal("Iachak03","LOCALS",0)
    SetGlobal("Iachak04","LOCALS",0)
    SetGlobal("Iachak06","LOCALS",0)
    SetGlobal("Iachak07","LOCALS",0)
    SetGlobal("Iachak08","LOCALS",0)
    SetGlobal("Iachak12","LOCALS",0)
    SetGlobal("Iachak13","LOCALS",0)
    SetGlobal("Iachak14","LOCALS",0)
    SetGlobal("Iaarmor","LOCALS",0)
    SetGlobal("Iaclw","LOCALS",0)
    SetGlobal("Iacsw","LOCALS",0)
    SetGlobal("Iacmw","LOCALS",0)
    SetGlobal("Iahard","LOCALS",0)
    SetGlobal("Iamiscast","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !Global("Iachak06","LOCALS",1)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  !Global("Iachak06","LOCALS",1)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  !Global("Iachak06","LOCALS",1)
  See(ThirdNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  !Global("Iachak06","LOCALS",1)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END