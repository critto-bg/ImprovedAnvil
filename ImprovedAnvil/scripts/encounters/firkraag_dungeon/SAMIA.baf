IF
  See([PC])
  OR(3)
    PartyHasItem("sw1h32") // ~Dragonslayer~
    PartyHasItem("shld21") // ~Dragon Scale Shield +2~
    Global("Iatomb","AR1202",1)
  Global("SamiaHostile","GLOBAL",0)
  Global("SpawnSamia","AR1202",1)
  Global("Iapcastts","GLOBAL",0)
THEN
  RESPONSE #100
    StartDialogNoSet(Player1)
END


IF
  !Allegiance(Myself,ENEMY)
  !PartyHasItem("sw1h32")  // ~Dragonslayer~
  !PartyHasItem("shld21")  // ~Dragon Scale Shield +2~
  Global("Iatomb","AR1202",0)
  Global("SamiaHostile","GLOBAL",0)
  Global("SpawnSamia","AR1202",0)
  Global("SamiaPreempted","GLOBAL",0)
  OR(3)
   AttackedBy([GOODCUTOFF],DEFAULT)
   SpellCastOnMe([GOODCUTOFF],0)
   HPPercentLT(Myself,100)
THEN
  RESPONSE #100
    Enemy()
    CreateCreatureObject("Akae",Myself,0,0,0)
    CreateCreatureObject("Kaol",Myself,0,0,0)
    CreateCreatureObject("Chak",Myself,0,0,0)
    CreateCreatureObject("Legdor",Myself,0,0,0)
    CreateCreatureObject("Deshar",Myself,0,0,0)
    ActionOverride("Akae",Enemy())
    ActionOverride("Kaol",Enemy())
    ActionOverride("Chak",Enemy())
    ActionOverride("Legdor",Enemy())
    ActionOverride("Deshar",Enemy())
    SetGlobal("SamiaPreempted","GLOBAL",1)
END

IF
  Allegiance(Myself,ENEMY)
  Global("SamiaBattle","AR1202",0)
THEN
  RESPONSE #100
    SetGlobal("SamiaBattle","AR1202",1)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("SamiaBattle","AR1202",1)
THEN
  RESPONSE #100
    Enemy()
END

IF
	Detect([PC])
	Global("Iatreasure","LOCALS",0)
	Global("Iaharmoniumplace","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("Iaharmoniumplace","GLOBAL",5)
		SetGlobal("Iatreasure","LOCALS",1)
		GiveItemCreate("wa2halb",Myself,0,0,0)  // Harmonium Halberd +3
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("partyfled","LOCALS",0)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("Iasamia03","LOCALS",0)
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  See(NearestEnemyOf(Myself))
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("potn35",Myself)  //
THEN
  RESPONSE #100
    SetGlobal("Iasamia03","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn35",Myself)  //
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  Global("Iasamia03","LOCALS",1)
  HasItem("minhp1",Myself)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    ReallyForceSpellRES("SPWII07",Myself)
    DestroyItem("minhp1")
    SetInterrupt(TRUE)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !CheckStatGT(Myself,0,SCRIPTINGSTATE9)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Iasamia01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iasamia01","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_HARDINESS)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iapcastts","GLOBAL",0)
  !HasItem("minhp1",Myself)
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("potn55",Myself)  // ~Potion of Superior Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn55",Myself)  // ~Potion of Superior Healing~
END

IF
  !HasItem("minhp1",Myself)
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !HasItem("minhp1",Myself)
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  HasItem("potn52",Myself)  // ~Potion of Extra Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn52",Myself)  // ~Potion of Extra Healing~
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  OR(2)
   Global("Iapcastts","GLOBAL",1)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
THEN
  RESPONSE #100
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),5)
  Global("Iasamia02","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iasamia02","LOCALS",1)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_GREATER_WHIRLWIND)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  Range(LastSeenBy(Myself),5)
  Global("Iasamia02","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iasamia02","LOCALS",2)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_CRITICAL_STRIKE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  Range(LastSeenBy(Myself),5)
  Global("Iasamia02","LOCALS",2)
THEN
  RESPONSE #100
    SetGlobal("Iasamia02","LOCALS",3)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_WHIRLWIND)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("DrankPotionTimer","LOCALS")
  See([PC])
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE5)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  Range(LastSeenBy(Myself),5)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_STUNNED)
  Global("Iasamia02","LOCALS",3)
THEN
  RESPONSE #100
    SetGlobal("Iasamia02","LOCALS",4)
    SetGlobalTimer("DrankPotionTimer","LOCALS",6)
    ForceSpell(Myself,WARRIOR_POWER_ATTACK)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("Iasamiadamaged","AR1202",0)
  OR(5)
   AttackedBy([GOODCUTOFF],DEFAULT)
   AttackedBy([PC],DEFAULT)
   SpellCastOnMe([GOODCUTOFF],0)
   SpellCastOnMe([PC],0)
   TookDamage()
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("Iasamiadamaged","AR1202",1)
    Shout(151)
    MoveToObject(Player1)
END

IF
  !Allegiance(Myself,ENEMY)
  InMyArea(Player1)
  Global("Iasamiadamaged","AR1202",1)
  Global("Iahostile","LOCALS",0)
THEN
  RESPONSE #100
    Enemy()
    SetGlobal("Iahostile","LOCALS",1)
    Shout(151)
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),90,RESISTSLASHING)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
  RandomNumGT(6,4)
  Range(LastSeenBy(Myself),2)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(SecondNearestEnemyOf(Myself))
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE6)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Iasamia01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",90)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Iasamia01","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  Global("Iasamia01","LOCALS",1)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iasamia01","LOCALS",0)
    SetGlobal("Iasamia02","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  !See([PC])
  !See([GOODCUTOFF])
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END