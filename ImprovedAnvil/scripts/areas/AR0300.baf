//The Docks //////////////

IF
  InMyArea(Player1)
  Global("Chapter","GLOBAL",6)
  Global("IADruidPlot","GLOBAL",9)
  Global("IAAmbushFirstTalk","GLOBAL",0)
  Global("IaDruMess02","AR0300",0)
THEN
  RESPONSE #100
    SetGlobal("IaDruMess02","AR0300",1)
    CreateCreatureObject("S!drume2",Player1,0,0,0)
END

IF
  PartyHasItem("S!robe01")
  Class(Player1,MAGE_ALL)
  Global("Iarobeforged","GLOBAL",1)
  Global("Ialimak","AR0300",0)
  Global("Iarobetimer","AR0300",0)
THEN
  RESPONSE #100
    SetGlobalTimer("Ialimakspawn","GLOBAL",THREE_DAYS)
    SetGlobal("Ialimak","AR0300",1)
    SetGlobal("Iarobetimer","AR0300",1)
END

IF
  Global("Ialimak","AR0300",1)
  GlobalTimerExpired("Ialimakspawn","GLOBAL")
  InMyArea(Player1)
  Class(Player1,MAGE_ALL)
  GlobalGT("Chapter","GLOBAL",5)
THEN
  RESPONSE #100
    SetGlobal("Ialimak","AR0300",2)
    CreateCreatureObjectDoor("S!limak",Player1,0,0,0) //
END

IF
  Global("IaLimakMet","GLOBAL",1)
  Global("IaLimakHostile","GLOBAL",1)
  RandomNumLT(6,3)
THEN
  RESPONSE #100
    SetGlobal("IaLimakMet","GLOBAL",0)
    CreateCreature("S!limak",[1648.1545],5)  //
END

IF
  Global("IaLimakMet","GLOBAL",1)
  Global("IaLimakHostile","GLOBAL",1)
  RandomNumLT(4,3)
THEN
  RESPONSE #100
    SetGlobal("IaLimakMet","GLOBAL",0)
    CreateCreature("S!limak",[3080.350],13)  //
END

IF
  Global("IaLimakMet","GLOBAL",1)
  Global("IaLimakHostile","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("IaLimakMet","GLOBAL",0)
    CreateCreature("S!limak",[3625.1299],9)  //
END

IF
  PartyHasItem("S!misc20")
  Global("Iabraintimer","AR0300",0)
THEN
  RESPONSE #100
    SetGlobalTimer("Iabrainrot","GLOBAL",THREE_DAYS)
    SetGlobal("Iabraintimer","AR0300",1)
END

IF
  Reputation(Player1,20)
  GlobalTimerExpired("Iavanyafir","GLOBAL")
  InMyArea(Player1)
  !GlobalGT("Iap1notgood","GLOBAL",0)
  OR(2)
   Global("FirkraagDead","GLOBAL",1)
   Dead("firkra02")
  Global("Iamesvan","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("Iamesvan","GLOBAL",1)
    CreateCreatureObjectOffScreen("S!messen",Player1,0,0,0) //
END

IF
  Global("Ianticheat","AR0300",0)
  Global("IaLimakHostile","GLOBAL",0)
  Global("Ialimak","AR0300",2)
  Exists("S!limak") //
  !Dead("S!limak") //
  !Allegiance("S!limak",ENEMY)
  HPPercentLT("S!limak",100)
THEN
  RESPONSE #100
    SetGlobal("Ianticheat","AR0300",1)
    ChangeEnemyAlly("S!limak",ENEMY)
    ApplySpellRES("SPWII07","S!limak")
END

IF
  InMyArea(Player1)
  Global("GorchOpen","GLOBAL",3)
  Global("Iasealonce","AR0300",0)
THEN
  RESPONSE #100
    SetGlobal("Iasealonce","AR0300",1)
    TriggerActivation("TRAN0301",FALSE)
    TriggerActivation("INFO301",TRUE)
END

IF
  InMyArea(Player1)
  Global("GorchOpen","GLOBAL",3)
  Global("Iasealonce","AR0300",1)
  Dead("maevar")
THEN
  RESPONSE #100
    SetGlobal("Iasealonce","AR0300",2)
    TriggerActivation("TRAN0301",TRUE)
    TriggerActivation("INFO301",FALSE)
END

IF
  InMyArea(Player1)
  Global("Iaforcekruin","GLOBAL",1)
  !PartyHasItem("misc8u") // Silver Blade
  Global("KruinTalked","GLOBAL",0)
  !Exists("Kruin")
THEN
  RESPONSE #100
    SetGlobal("Iaforcekruin","GLOBAL",2)
    CreateCreatureObjectOffScreen("KRUIN",Player1,0,0,0) //
END

IF
  InMyArea(Player1)
  Global("Iacromwellshut","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("Iacromwellshut","GLOBAL",2)
    TriggerActivation("TRAN0334",FALSE)
END

IF
  InMyArea(Player1)
  Global("Iacromwellshut","GLOBAL",2)
  Dead("KRUIN")
  CombatCounter(0)
THEN
  RESPONSE #100
    SetGlobal("Iacromwellshut","GLOBAL",3)
    TriggerActivation("TRAN0334",TRUE)
END

IF
  InMyArea(Player1)
  Global("Iacromwellshut","GLOBAL",2)
  Global("IaGaveKruinSword","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("Iacromwellshut","GLOBAL",3)
    TriggerActivation("TRAN0334",TRUE)
END

IF
  GlobalTimerExpired("KPPlotTime","GLOBAL")
  InMyArea(Player1)
  GlobalGT("Chapter","GLOBAL",5)
  Global("KeepPlot","GLOBAL",4)
  InParty("Nalia")
  Global("NaliaIsaeaPlotDone","GLOBAL",1)
  Global("KPRunnerSpawn","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("KPRunnerSpawn","GLOBAL",1)
    CreateCreatureObjectOffScreen("KPRUNN01",Player1,0,0,0)
END

IF
  GlobalTimerExpired("KPPlotTime","GLOBAL")
  InMyArea(Player1)
  GlobalGT("Chapter","GLOBAL",5)
  Global("KeepPlot","GLOBAL",4)
  !InParty("Nalia")
  Global("KPRunnerSpawn","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("KPRunnerSpawn","GLOBAL",1)
    CreateCreatureObjectOffScreen("KPRUNN01",Player1,0,0,0)
END

IF
  GlobalTimerExpired("KPPlotTime","GLOBAL")
  InMyArea(Player1)
  GlobalGT("Chapter","GLOBAL",5)
  Global("KeepPlot","GLOBAL",7)
  Global("KPRunnerSpawn","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("KPRunnerSpawn","GLOBAL",2)
    CreateCreatureObjectOffScreen("KPRUNN01",Player1,0,0,0)
END

IF
  GlobalTimerExpired("KPPlotTime","GLOBAL")
  InMyArea(Player1)
  GlobalGT("Chapter","GLOBAL",5)
  Global("KeepPlot","GLOBAL",8)
  Global("KPRunnerSpawn","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("KPRunnerSpawn","GLOBAL",3)
    CreateCreatureObjectOffScreen("KPRUNN01",Player1,0,0,0)
END