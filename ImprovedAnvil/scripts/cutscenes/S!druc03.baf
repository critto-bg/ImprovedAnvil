// Extended druid stronghold, great druid arrives to save the day during the attack on Trademeet //

IF
  True()
THEN
  RESPONSE #100
    CutSceneId(Player1)

    Wait(1)
    MoveViewObject(Myself,INSTANT)
    Wait(2)

    DisplayStringHead(Player1,@5406)
    Wait(1)
    ActionOverride("S!trgu01",EscapeArea())
    Wait(3)

    FadeToColor([20.0],0)
    Wait(1)

    CopyGroundPilesTo("AR2000",[1820.1520])
    ActionOverride("S!jagdu",MoveBetweenAreas("AR2000",[2830.1570],0))
    LeaveAreaLUAPanic("AR2000","",[2020.1410],0)
    LeaveAreaLUA("AR2000","",[2020.1410],0)
    ActionOverride(Player2,LeaveAreaLUA("AR2000","",[1860.1425],0))
    ActionOverride(Player3,LeaveAreaLUA("AR2000","",[2015.1320],0))
    ActionOverride(Player4,LeaveAreaLUA("AR2000","",[1760.1445],0))
    ActionOverride(Player5,LeaveAreaLUA("AR2000","",[1850.1505],0))
    ActionOverride(Player6,LeaveAreaLUA("AR2000","",[2080.1310],0))
    Wait(1)

    CreateCreature("s!gdru01",[2700.1215],0)
    CreateCreature("s!tslead",[2780.1258],0)
    CreateCreature("s!logan",[1925.1360],0)

    MoveViewObject(Myself,INSTANT)

    FaceObject("s!logan")
    ActionOverride("s!logan",FaceObject(Player1))

    ActionOverride(Player2,FaceObject("s!logan"))
    ActionOverride(Player3,FaceObject("s!logan"))
    ActionOverride(Player4,FaceObject("s!logan"))
    ActionOverride(Player5,FaceObject("s!logan"))
    ActionOverride(Player6,FaceObject("s!logan"))

    ActionOverride("S!jagdu",MoveToPointNoInterrupt([2100.1445]))
    ActionOverride("S!jagdu",FaceObject(Player1))

    SetGlobal("IADruidSiege","GLOBAL",3)
    SetGlobal("IADruidPlot","GLOBAL",5)

    FadeFromColor([20.0],0)
    Wait(2)
    ActionOverride("s!logan",DisplayStringHead(Myself,@5407))
    Wait(3)

    ActionOverride("S!gdru01",MoveToPointNoInterrupt([2180.1315]))
    ActionOverride("S!gdru01",FaceObject("S!jagdu"))
    ActionOverride("S!tslead",MoveToPointNoInterrupt([2235.1420]))
    ActionOverride("S!tslead",FaceObject("S!jagdu"))

    Wait(1)
    ActionOverride("s!gdru01",DisplayStringHead(Myself,@5408))
    Wait(3)
    ActionOverride("s!gdru01",DisplayStringHead(Myself,5409))
    Wait(3)
    ActionOverride("s!jagdu",DisplayStringHead(Myself,@5410))
    Wait(3)
    ActionOverride("s!jagdu",DestroyItem("minhp1"))
    ActionOverride("s!jagdu",Kill(Myself))

    // TODO
    // jagdu's death needs to be better animated with spell effects, etc.
    // scripted werewolves run away
    // corpith says something about making sure they flee and escapes, along with all the scripted guards

    Wait(2)

    MultiPlayerSync()
    ActionOverride("s!gdru01",StartDialogNoSet(Player1))
    EndCutSceneMode()
END